//plugins {
//  id "de.gesellix.docker" version "2016-09-09T17-53-52"
//}
apply plugin: 'java'

version = '1.0.0'
sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
//    jcenter()
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly 'javax.servlet:servlet-api:2.5'
    compileOnly 'commons-lang:commons-lang:2.6'
    compileOnly ('edu.internet2.middleware.grouper:grouper:2.3.0') {
        transitive = false
    }
    compileOnly ('edu.internet2.middleware.grouper:grouper-ui:2.3.0') {
        transitive = false
    }
    compileOnly 'edu.internet2.middleware.grouper:subject:2.3.0'
    compileOnly 'org.slf4j:slf4j-api:1.6.2'
    compileOnly 'org.hibernate:hibernate-core:5.0.4.Final'
}

//docker {
//    dockerHost = System.env['DOCKER_HOST']?.replace("tcp", "http");
//}

task copyDocker(type: Copy) {
    from 'src/test/docker-compose/'
    into 'build/docker-compose/'
}

task copyJspToDocker(type: Copy) {
    dependsOn copyDocker

    from 'src/main/webapp/'
    into 'build/docker-compose/grouper/webapp'
}

task addLib(type: Copy) {
    dependsOn jar

    from 'build/libs/'
    into 'build/docker-compose/grouper/lib/'
}

task buildImage(type: Exec) {
    dependsOn copyDocker, copyJspToDocker, addLib

    doFirst {
        logger.lifecycle("Building the initial images may take a long time. Have plenty of bandwidth.")
    }

    workingDir 'build/docker-compose'
    commandLine 'docker-compose', 'build'
}

task runContainer(type: Exec) {
    dependsOn buildImage

    workingDir 'build/docker-compose'
    commandLine 'docker-compose', 'up', '-d'
}

task stopContainer(type: Exec) {
    workingDir 'build/docker-compose'
    commandLine 'docker-compose', 'kill'
}

task removeContainer(type: Exec) {
    dependsOn stopContainer

    workingDir 'build/docker-compose'
    commandLine 'docker-compose', 'rm', '-f'
}

task resetContainer() {
    dependsOn removeContainer
}

clean {
    dependsOn resetContainer
}
